

	 --                  RECURRENT RBF PREDICTOR
	 --     predicts Lorenz-attractor time series
	 --------------------------------------------------------------
	 display N3 |  display C8 |  display R |  scale=2
	 TMAX=25 |  DT=0.0001 |  NN=TMAX/DT+1
	 --------------------------------------------------------------
	 m=50 |  -- predictor delay (try larger m, too!)
	 ARRAY signal[m]
	 --
	 nv=20
	 ARRAY x[1],error[1]
	 ARRAY v[nv]+v0[1]+y[1]=vv |  v0[1]=1 |  -- with bias
	 ARRAY WW[1,nv+2]
	 --
	 for k=1 to nv+2 |  -- initialize
	   WW[1,k]=0.1*ran()
	   next
	 ---------------------------------------------------------
	 ARRAY XX[nv-1] |  --   radial-basis centers
	 for i=1 to nv-1
	   XX[i]=2*i/nv-1
	   next
	 bb=2/(nv^2)
	 ---------------------------------------------------------
	 lrate=0.001 |  Tnoise=0.0
	 b=28 |  c=2.6667 |  A=10 |  -- for Lorenz system
	 -----------------------------------------------------------
	 N=8 |  --                number of training runs
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for prediction test' |  STOP
	 lrate=0 |  drun  |  -- test run
	 ------------------------------------------------------------
	 DYNAMIC
	 ------------------------------------------------------------
	 d/dt X=A*(Y-X)+0.001 |  --   Lorenz attractor
	 d/dt Y=X*(b-Z)-Y
	 d/dt Z=X*Y-c*Z
	 ---------------------------------------------------------------
	 OUT  |  --   we use sampled data from now on!
	 target=0.025*X
	 Vector signal=signal{-1} |  signal[1]=target
	 x[1]=signal[m]
	 --
	 Vector v=exp(bb*(x[1]-X)*(X-x[1])) |  -- rb functions
	 Vector y=WW*vv
	 --
	 error[1]=target-y[1]
	 DELTA WW=lrate*error*vv
	 -----------------------------       stripchart-type display
	 yy=y[1]+0.5*scale |  TARGET=target+0.5*scale
	 error1=error[1]-0.5*scale
	 dispt yy,TARGET,error1

