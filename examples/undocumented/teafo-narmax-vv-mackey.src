

	 --                                NARMAX PREDICTOR
	 --     predicts Mackey-Glass chaos generator
	 ------------------------------------------------------------------
	 display N4 |  display C8 |  display R |  scale=2
	 TMAX=500 |  DT=0.04 |  NN=12500
	 a=0.2 |  b=0.1 |  c=10 |  --        for Mackey-Glass
	 tau=25
	 ARRAY DD[1000] |  --       time-delay buffer
	 s=10 |  --           initialize delay buffer
	 --------------------------------------------------------------
	 m=50 |  --                        predictor delay       
	 nx=20 |  --       number of input-line neurons
	 nf=10 |  --       number of feedback-line neurons
	 nv=17 |  --      number of hidden-layer neurons
	 --
	 ARRAY signal[m],y[1],error[1]
	 ARRAY x[nx]+x0[1]+feedback[nf]=xx |  x0[1]=1
	 ARRAY v[nv]+v0[1]=vv |  v0[1]=1
	 ARRAY deltav[nv]+deltav0[1]=deltavv |  deltav0[1]=1
	 ARRAY WW1[nv,nx+nf+1],WW2[1,nv+1]
	 --
	 for i=1 to nv |  for k=1 to nx+nf+1 |  -- initialize
	     WW1[i,k]=0.1*ran()
	     next  |  next
	 -----------------------------------------------------------
	 lrate1=0.0003 |  lrate2=0.0001
	 Tnoise=0.00
	 --
	 N=38 |  --                         number of training runs
	 Switch=1 |  --    teacher forcing
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for prediction tests' |  STOP
	 Switch=0
	 lrate1=0 |  lrate2=0 |  drun  |  -- test run
	 -----------------------------------------------------------
	 DYNAMIC
	 ----------------------------------------------------------
	 tdelay Sd=DD,s,tau |  -- Mackey-Glass time series
	 d/dt s=a*Sd/(1+Sd^c)-b*s
	 OUT  |  -- sampled data from here on!
	 target=s-0.75
	 ---------------------------------------------------------------
	 Vector signal=signal{-1} |  signal[1]=target+Tnoise*ran()
	 Vector x=x{-1} |  x[1]=signal[m] |  --     input delay line
	 Vector v=tanh(WW1*xx) |  --                          note bias
	 Vector y=WW2*vv |  --              no limiter needed on output!
	 --
	 Vector feedback=feedback{-1}
	 feedback[1]=Switch*target+(1-Switch)*y[1]
	 --
	 Vector error=target-y
	 Vector deltavv=WW2%*error*(1-vv^2)
	 DELTA WW1=lrate1*deltav*xx
	 DELTA WW2=lrate2*error*vv
	 ----------------------------------------  stripchart-type display
	 Y=y[1]+0.5*scale
	 TARGET=target+0.5*scale
	 errorx5=5*error[1]-0.5*scale
	 ss=signal[m]+0.5*scale
	 dispt Y,errorx5,TARGET

