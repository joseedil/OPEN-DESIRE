

	 --     JORDAN PREDICTOR
	 --     predicts Mackey-Glass chaos generator
	 -------------------------------------------------------------
	 display N3 |  display C8 |  display R |  scale=1
	 -------------------------------------------------------------
	 TMAX=300 |  DT=0.04 |  NN=7500
	 a=0.2 |  b=0.1 |  c=10 |  --   for Mackey-Glass
	 tau=25 |  s=10 |  --         s=10 initializes buffer
	 ARRAY DD[1000] |  --           time-delay buffer
	 --------------------------------------------------------------
	 m=50 |  --                                 predictor delay
	 nx=1 |  --                                        single input
	 nv=19 |  --   number of hidden-layer neurons
	 TWOPI=2*PI
	 --
	 ARRAY signal[m],error[1]
	 --
	 --  input, bias, hidden layer (context layer)
	 ARRAY x[nx]
	 ARRAY f1[nx]+f2[nx]+f3[nx]+f4[nx]+f5[nx]+y[1]=f
	 ARRAY v[nv],deltav[nv]
	 ARRAY WW1[nv,5*nx+1],W2[1,nv]
	 --
	 for i=1 to nv |  for k=1 to 5*nx+1 |  -- initialize
	     WW1[i,k]=0.1*ran()
	     next  |  next
	 -----------------------------------------------------------
	 lrate1=0.001 |  lrate2=0.0005
	 Tnoise=0.0
	 --
	 N=8 |  --                   number of training runs
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for prediction tests' |  STOP
	 lrate1=0 |  lrate2=0 |  drun  |  -- test run
	 write "delay is ";TMAX*m/NN
	 -----------------------------------------------------------
	 DYNAMIC
	 -----------------------------------------------------------
	 tdelay Sd=DD,s,tau |  -- Mackey-Glass time series
	 d/dt s=a*Sd/(1+Sd^c)-b*s
	 OUT  |  --                   sampled data from here on!
	 target=0.5*s
	 --------------------------------- 
	 Vector signal=signal{-1} |  signal[1]=target+Tnoise*ran()
	 x[1]=signal[m]
	 Vector f1=x
	 Vector f2=sin(PI*x) |  Vector f3=sin(TWOPI*x)
	 Vector f4=cos(PI*x) |  Vector f5=cos(TWOPI*x)
	 Vector v=tanh(WW1*f) |  --              includes bias
	 Vector y=W2*v |  --    no limiter needed on output!
	 --
	 Vector error=target-y
	 Vector deltav=W2%*error*(1-v^2) |  -- backprop
	 DELTA WW1=lrate1*deltav*f
	 DELTA W2=lrate2*error*v
	 -----------------------------
	 ersqx20=20*error[1]^2-0.5*scale
	 errorx5=5*error[1]-0.5*scale
	 dispt y[1],ersqx20,target,errorx5

