

	 --                                        DIGITAL FILTER
	 --  H = (z^n + 1)/(z^n ) 
	 -- successive samples of the filter output are stored
	 --      in an array that will be used to compute the FFT
	 ---------------------------------------------------------
	 display N1 |  display C8 |  display R
	 NN=16384
	 -- 
	 n=10 |  ARRAY x[n],a[n],b[n]
	 ARRAY OUTPUTx[NN],OUTPUTy[NN] |  -- array for FFT
	 ------------------------------------------------ 	
	 --          specify filter parameters 	 
	 b[1]=0.75 |  -- other a[i], b[i] default to 0
	 bb=1 |  --           feedforward coefficient
	 --------------------------------------------
	 t=0 |  --   (default initial t would be t=1)
	 drunr
	 write 'type go for FFT' |  STOP
	 -----------------------------------
	 FFT F,NN,OUTPUTx,OUTPUTy
	 drunr FFT |  --    drunr resets t=1
	 write 'type go for amplitude/phase display' |  STOP
	 scale=2
	 drun SECOND |  -- amplitude/phase display
	 ---------------------------------------------------------
	 DYNAMIC
	 ---------------------------------------------------------
	 input=swtch(1-t) |  -- this produces the impulse response
	 output=x[n]+bb*input |  --      note feedforward term
	 Vector x=x{-1}+b*input-a*output
	 store OUTPUTx=output |  -- fill FFT arrays
	 dispt output
	 ---------------------------------------------------------
	    label FFT
	 get xx=OUTPUTx |  get yy=OUTPUTy |  --    FFT arrays
	 outx=0.5*xx
	 outy=0.5*(yy-scale) |  --            stripchart display
	 dispt outx,outy
	 ----------------------------   AMPLITUDE/PHASE DISPLAY
	    label SECOND
	 get xx=OUTPUTx |  get yy=OUTPUTy |  --    FFT arrays
	 r=sqrt(xx^2+yy^2)
	 phi=atan2(yy,xx)-0.5*scale |  --     offset
	 dispt r,phi

