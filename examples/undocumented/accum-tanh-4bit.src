

	 --           4-BIT ENCODER  BACKPROP/BATCH
	 ----------------------------------------------------------------
	 display N0 |  display C8 |  display R |  scale=5
	 ------------------------------------------------
	 nv=11 |  --             number of hidden-layer neurons
	 n=16 |  --                        number of network inputs
	 G=0.95 |  --                                target amplitude
	 --
	 ARRAY x[n]+x0[1]=xx |  x0[1]=1 |  -- input and bias
	 ARRAY v[nv],deltav[nv] |  --               hidden layer
	 ARRAY y[n],error[n],deltay[n] |  --           second layer	
	 ARRAY W1[nv,n+1],W2[n,nv],DW1[nv,n+1],DW2[n,nv]
	 --	 
	 ARRAY INPUT[n,n]
	 --
	 --      set input and target amplitudes                        
	 for i=1 to n |  for k=1 to n
	     INPUT[i,k]=-G
	     next
	   INPUT[i,i]=G
	   next
	 --
	 --    small random initial weights "break symmetry"
	 --
	 for i=1 to nv |  for k=1 to n+1
	     W1[i,k]=0.1*ran() |  next  |  next
	 for i=1 to n |  for k=1 to nv
	     W2[i,k]=0.1*ran() |  next  |  next
	 -----------------------------------------------------------
	 --                                set experiment parameters
	 lrate1=0.01 |  lrate2=0.004
	 mom1=0.2 |  mom2=0.2
	 NN=1000
	 --
	 drun  |  --   make a simulation run (NN steps)
	 -----------------------------------------------------------
	 DYNAMIC
	 -----------------------------------------------------------
	 --                                      weight-learning run
	 iRow=t |  Vector x=INPUT#
	 Vector v=tanh(W1*xx)
	 Vector y=tanh(W2*v)
	 Vectr delta error=x-y
	 DOT mse=error*error
	 --------------------------------
	 SAMPLE n
	 Vector deltay=error*(1-y^2)
	 Vector deltav=W2%*deltay*(1-v^2)
	 --                                                backprop with mo
	 MATRIX DW1=lrate1*deltav*xx+mom1*DW1
	 MATRIX DW2=lrate2*deltay*v+mom2*DW2
	 DELTA W1=DW1 |  DELTA W2=DW2
	 ----------------------------------------------------------
	 DOT enormsqr=error*error
	 Vector error=0
	 ENORMSQR=enormsqr-scale |  --            offset display curve
	 dispt ENORMSQR |  --                    display error measure

