

	 --    4-BIT ENCODER LEARNING, BACKPROPAGATION
	 -----------------------------------------------------------
	 display N1 |  display C8 |  display R |  --        display
	 scale=2 |  cc=8 |  CC=2
	 ------------------------------------------------
	 nv=11 |  --                   number of hidden-layer neurons
	 n=16 |  --                          number of network inputs
	 G=0.95 |  --                                target amplitude
	 --
	 ARRAY x[n]+x0[1]+y[n]=xx |  x0[1]=1 |  -- input and bias
	 ARRAY v[nv],deltav[nv] |  --               hidden layer
	 ARRAY error[n],deltay[n] |  --      output layer	
	 --	 
	 ARRAY WW1[nv,2*n+1],WW2[n,nv],DWW1[nv,2*n+1],DWW2[n,nv]
	 --	 
	 ARRAY INPUT[n,n]
	 ARRAY LAYER1[n],LAYER2[n]
	 --
	 --      reduce input and target amplitudes                        
	 for i=1 to n |  for k=1 to n
	     INPUT[i,k]=-G
	     next
	   INPUT[i,i]=G
	   next
	 --
	 --    small random initial weights "break symmetry"
	 --
	 for i=1 to nv |  for k=1 to 2*n+1
	     WW1[i,k]=0.1*ran() |  next  |  next
	 for i=1 to n |  for k=1 to nv
	     WW2[i,k]=0.1*ran() |  next  |  next
	 -----------------------------------------------------------
	 --                                set experiment parameters
	 lrate1=0.1 |  lrate2=0.08
	 mom1=0.2 |  mom2=0.2
	 NN=2000
	 --
	 drun  |  --   make a simulation run (NN steps)
	 -----------------------------------------------------------
	 DYNAMIC
	 -----------------------------------------------------------
	 --                                      weight-learning run
	 iRow=t |  Vector x=INPUT#
	 Vector v=tanh(WW1*xx)
	 Vector y=tanh(WW2*v)
	 ------------------------------------------------------------
	 Vector error=x-y |  --                backpropagation
	 Vector deltay=error*(1-y^2)
	 Vector deltav=WW2%*deltay*(1-v^2)
	 --                                          "moment" method
	 MATRIX DWW1=lrate1*deltav*xx+mom1*DWW1
	 MATRIX DWW2=lrate2*deltay*v+mom2*DWW2
	 DELTA WW1=DWW1 |  DELTA WW2=DWW2
	 ----------------------------------------------------------
	 DOT enormsqr=error*error |  --          accumulate enormsqr
	 ENORMSQR=enormsqr-scale |  --            offset display curve
	 dispt ENORMSQR |  --                    display error measure
	 --
	 --                            SHOW display of layers
	 if t-0.996*TMAX
	   Vector LAYER1=cc*swtch(x)+CC
	   Vector LAYER2=cc*swtch(y)+CC
	   SHOW  |  SHOW LAYER1 |  SHOW LAYER2

