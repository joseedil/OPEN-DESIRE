

	 --     NONLINEAR RECURRENT PREDICTOR
	 --     predicts Lorenz-attractor time series
	 ---------------------------------------------------------------
	 display N3 |  display C8 |  display R |  scale=3
	 TMAX=15 |  DT=0.002 |  NN=TMAX/DT+1
	 --------------------------------------------------------------
	 m=200 |  -- predictor delay (try larger m, too!)
	 nv=17 |  --      number of hidden-layer neurons
	 --
	 ARRAY signal[m] |  --  for prediction  delay
	 --
	 ARRAY x[1]+x0[1]+v[nv]+error[1]=xx |  x0[1]=1
	 ARRAY deltav[nv],y[1],target[1]
	 ARRAY WW1[nv,nv+3],WW2[1,nv]
	 --
	 for i=1 to nv |  for k=1 to nv+3 |  -- initialize
	     WW1[i,k]=0.1*ran()
	     next  |  next
	 -----------------------------------------------------------
	 lrate1=0.001 |  lrate2=0.0007
	 --
	 --        parameters for Lorenz system
	 b=28 |  c=2.6667 |  A=10
	 --------------------------------------------------------------	  
	 N=9 |  --                        number of training runs
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for prediction tests' |  STOP
	 lrate1=0 |  lrate2=0 |  drun  |  -- test run
	 ---------------------------------------------------------------
	 DYNAMIC
	 ---------------------------------------------------------------
	 d/dt X=A*(Y-X)+0.001 |  --     Lorenz attractor
	 d/dt Y=X*(b-Z)-Y
	 d/dt Z=X*Y-c*Z
	 ---------------------------------------------------------------
	 OUT  |  --         use sampled data from now on!
	 target[1]=0.05*X
	 Vector signal=signal{-1} |  signal[1]=target[1]
	 x[1]=signal[m]
	 --	 
	 invoke backprop(xx,v,deltav,y,error,target,WW1,WW2,lrate1,lrate2)
	 --
	 -----------------------------     stripchart-type dispay
	 yy=y[1]+0.5*scale |  TARGET=target[1]+0.5*scale
	 errorx10=10*error[1]-0.5*scale
	 dispt yy,TARGET,errorx10

