

	 --                                 COMPETITIVE LEARNING
	 -------------------------------------------------------
	 m=2 |  --                              number of inputs
	 nn=3 |  N=nn*nn |  --                number of patterns
	 n=9 |  --             no. of competitive-layer neurons
	 --
	 ARRAY x[m],v[n],h[n],z[m],W[n,m],INPUT[N,2],bias[m]
	 bias[2]=0.2
	 -------------------------------------------------------
	 --              set up uniformly spaced cluster centers
	 s=0.5
	 for i=1 to nn |  for k=1 to nn
	     INPUT[i+(k-1)*nn,1]=s*(1+nn-2*i)/(1-nn)
	     INPUT[i+(k-1)*nn,2]=s*(1+nn-2*k)/(1-nn)
	     next  |  next
	 -------------------------------------------------------
	 for i=1 to n
	   for k=1 to m |  --        set random initial weights
	     W[i,k]=ran()
	     next
	   h[i]=0 |  --          initialize conscience (if any)
	   next
	 display Q |  display N1 |  display C8
	 ------------------------------------------------------
	 --                           set experiment parameters
	 crit=0 |  --                             CLEARN  mode
	 lrate=0.02 |  kappa=0.995 |  lrate0=0.00002
	 noise=0.16
	 NN=18000
	 ------------------------------------------------------
	 drun  |  --             "continued" runs show progress
	 drun  |  drun  |  drun
	 write 'type go for a check run' |  STOP
	 ------------------------------------------------------
	 --                                   make a recall run
	 lrate=0 |  lrate0=0
	 for i=1 to n |  h[i]=1.0E-25 |  next
	 drun
	 ONE=0 |  H=0 |  --     initialize checksum and entropy
	 a=1/NN
	 for i=1 to n
	   h[i]=a*h[i] |  --   statistical relative frequencies
	   ONE=ONE+h[i] |  --               accumulate checksum
	   H=H-h[i]*ln(h[i]) |  --           accumulate entropy
	   next
	 -----------------------------          display results
	 write h |  write
	 write 'H =  ';H/ln(2);' bits','      checksum =  ';ONE
	 write 'log2(N) = ';ln(N)/ln(2)
	 STOP
	 ------------------------------------------------------
	    label eee
	 --                            parameter-editing screen
	 edit 260-270,295,2050-2090
	 ------------------------------------------------------
	 DYNAMIC
	 ------------------------------------------------------
	 --                                 weight-learning run
	 lrate=lrate*kappa+lrate0 |  --     decrease learn rate
	 iRow=t
	 Vector x=noise*ran()+noise*ran()+noise*ran()+INPUT#+bias
	 CLEARN v=W(x)lrate,crit |  --            compete,learn
	 Vectr delta h=v |  --                               FSCL when cri
	 Vector z=W%*v |  --              reconstruct templates
	 DISPXY z[1],z[2],x[1],x[2]

