

	 --    ONE-LAYER NONLINEAR PREDICTOR
	 --         predicts Mackey-Glass  time series
	 -------------------------------------------------------------
	 display N14 |  display C7 |  display R |  scale=1
	 -------------------------------------------------------------
	 TMAX=300 |  DT=0.04 |  NN=TMAX/DT+1
	 a=0.2 |  b=0.1 |  c=10 |  --   for Mackey-Glass
	 tau=25 |  s=10 |  --         s=10 initializes buffer
	 ARRAY DD[1000] |  --           time-delay buffer
	 -------------------------------------------------------------
	 m=50 |  --                                predictor delay 
	 nx=10 |  --             number of delay-line taps
	 ARRAY signal[m]
	 ARRAY x[nx]
	 ARRAY y[1],error[1],W[1,nx]
	 --
	 for k=1 to nx |  --      initialize
	   W[1,k]=0.1*ran()
	   next
	 -------------------------------------    parameters
	 lrate=0.001
	 Tnoise=0.00 |  --               try more noise!
	 --
	 N=8 |  --                   make N training runs
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for prediction tests' |  STOP
	 lrate=0 |  drun  |  drun  |  -- test runs
	 -----------------------------------------------------------
	 DYNAMIC
	 -----------------------------------------------------------
	 tdelay Sd=DD,s,tau |  -- Mackey-Glass
	 d/dt s=a*Sd/(1+Sd^c)-b*s
	 OUT  |  --        sampled data from here on!
	 target=0.5*s-0.4
	 ---------------------------------------------------------------
	 Vector signal=signal{-1}
	 signal[1]=target+Tnoise*ran()
	 Vector x=x{-1} |  x[1]=signal[m] |  -- delay line
	 Vector y=W*x
	 --
	 Vector error=target-y |  --     LMS Algorithm
	 DELTA W=lrate*error*x
	 -------------------------    stripchart-type display  
	 errorx5=5*error[1]-0.5*scale
	 Y=y[1]+0.5*scale |  Target=target+0.5*scale
	 dispt Y,errorx5,Target

