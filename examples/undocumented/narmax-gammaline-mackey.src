

	 --   NONLINEAR  NARMAX/GAMMA-LINE PREDICTOR
	 --     predicts Mackey-Glass chaos generator
	 ------------------------------------------------------------------
	 display N3 |  display C8 |  display R |  scale=1
	 TMAX=500 |  DT=0.04 |  NN=TMAX/DT+1
	 a=0.2 |  b=0.1 |  c=10 |  --        for Mackey-Glass
	 tau=25
	 ARRAY DD[1000] |  --       time-delay buffer
	 s=10 |  --           initialize delay buffer
	 --------------------------------------------------------------
	 m=50 |  --                        predictor delay       <****
	 nx=10 |  --     number of gamma-line neurons
	 nf=10
	 nv=17 |  --      number of hidden-layer neurons
	 --
	 ARRAY signal[m],y[1],error[1]
	 ARRAY x[nx]+x0[1]+feedback[nf]=xx |  x0[1]=1
	 ARRAY v[nv],vdelta[nv]
	 ARRAY WW1[nv,nx+nf+1],W2[1,nv]
	 --
	 for i=1 to nv |  for k=1 to nx+nf+1 |  -- initialize
	     WW1[i,k]=0.1*ran()
	     next  |  next
	 -----------------------------------------------------------
	 lrate1=0.006 |  lrate2=0.0007
	 Tnoise=0.00
	 mu=0.0006 |  --                for gamma delay line
	 --
	 N=6 |  --                           number of training runs
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for prediction test' |  STOP
	 lrate1=0 |  lrate2=0 |  drun  |  -- test run
	 -----------------------------------------------------------
	 DYNAMIC
	 ----------------------------------------------------------
	 tdelay Sd=DD,s,tau |  -- Mackey-Glass time series
	 d/dt s=a*Sd/(1+Sd^c)-b*s
	 ---------------------------------------------------------------
	 OUT  |  -- sampled data from here on!
	 target=0.5*s-0.4
	 Vector signal=signal{-1} |  signal[1]=target+Tnoise*ran()
	 Vector x=x+mu*(x{-1}-x) |  x[1]=signal[m] |  --  gamma line
	 Vector v=tanh(WW1*xx) |  --                          note bias
	 Vector y=W2*v |  --              no limiter needed on output!
	 --
	 Vector error=target-y
	 --
	 Vector feedback=feedback+mu*(feedback{-1}-feedback)
	 feedback[1]=y[1]
	 --
	 Vector vdelta=W2%*error*(1-v^2)
	 --
	 DELTA WW1=lrate1*vdelta*xx
	 DELTA W2=lrate2*error*v
	 ------------------------------------------ stripchart-type display
	 yy=y[1]+0.5*scale |  TARGET=target+0.5*scale
	 errorx5=5*error[1]-0.5*scale
	 ss=signal[m]+0.5*scale
	 dispt yy,errorx5,TARGET

