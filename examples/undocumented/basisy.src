

	 --                           FUZZY-BASIS-FUNCTION NETWORK
	 ------------------------------------------------------------------
	 FUNCTION TGT(p,q)=sin(p)*cos(q) |  -- (or other function)  
	 ------------------------------------------------------------------
	 --                                 compute membership functions
	 --
	 ARRAY XX$[1] |  -- dummy peak-ordinate array
	 ARRAY mb$[1],MEMBER$[1] |  --  dummy membership arrays
	 --
	 SUBMODEL fuzzmemb(n$,XX$,mb$,MEMBER$,input$)
	   Vector mb$=SAT((XX$-input$)/(XX$-XX${1})) |  mb$[n$+1]=0
	   Vector MEMBER$=mb$-mb${1}
	   end
	 -----------------------------
	 n1=10
	 ARRAY X1dum[1]+X1[n1]=XX1 |  -- peak ordinates
	 X1dum[1]=-1E+25 |  --                              dummy left end
	 ARRAY mb1[n1+1] |  --                   membership functions
	 ARRAY mbr1[n1]+M1dum[1]=MEMBER1
	 ----------------------------- 
	 n2=12
	 ARRAY X2dum[1]+X2[n2]=XX2 |  -- peak ordinates
	 X2dum[1]=-1.0E+25 |  --                         dummy left end!
	 ARRAY mb2[n2+1] |  --                   membership functions
	 ARRAY mbr2[n2]+M2dum[1]=MEMBER2
	 --------------------------------------------------------
	 ARRAY x1[1]+x2[1]=x,y[1],error[1] |  --    input variables, error
	 ARRAY W1[n1,1],v1[n1],h1[n1]
	 ARRAY W2[n2,1],v2[n2],h2[n2]
	 ARRAY M12[n1,n2]=m12
	 ARRAY W3[1,n1*n2]
	 ARRAY z1[1],z2[1]
	 ---------------------------------------------------------
	 display N1 |  --                      set display colors
	 -------------                                  set parameters
	 lratex=0.2 |  kappa=0.9996 |  lrate0=0 |  C=0.2
	 crit=0 |  --                 (crit=0 for FSCL conscience)
	 NN=30000
	 ------------------------------------------------------------------
	 --
	 drun COMPETE
	 ------------------------------------    sort routine
	 for i=1 to n1
	   X1[i]=1.0E+25
	   for k=1 to n1
	     if W1[k,1]<X1[i] then X1[i]=W1[k,1] |  K1=k |  else proceed
	     next
	   W1[K1,1]=1.0E+25
	   next
	 ------------------
	 for i=1 to n2
	   X2[i]=1.0E+25
	   for k=1 to n2
	     if W2[k,1]<X2[i] then X2[i]=W2[k,1] |  K2=k |  else proceed
	     next
	   W2[K2,1]=1.0E+25
	   next
	 --------------------
	 ARRAY b1[n1+1],b2[n2+1]
	 for i=1 to n1 |  b1[i]=C/(XX1[i]-XX1[i+1]) |  next  |  --      
	 for i=1 to n2 |  b2[i]=C/(XX2[i]-XX2[i+1]) |  next
	 ---------------------------------------------------------
	 lratef=0.4 |  kappa=1
	 drun  |  -- function-learning run
	 NN=30000 |  lratex=0 |  lratef=0 |  lrate0=0 |  --    recall r
	 drun
	 --------------------------------------------------------------
	 DYNAMIC
	 --------------------------------------------------------------
	 lratef=kappa*lratef+lrate0 |  --     reduce learning rate
	 --
	 Vector x=ran()
	 tgt=TGT(x1[1],x2[1])
	 ------------
	 --		compute membership functions
	 --
	 Vector mb1=b1*(XX1-x1[1]) |  Vector mb1=atan(mb1)
	 Vector MEMBER1=mb1-mb1{1}
	 Vector mb2=b2*(XX2-x2[1]) |  Vector mb2=atan(mb2)
	 Vector MEMBER2=mb2-mb2{1}
	 --  invoke fuzzmemb[n1,XX1,mb1,MEMBER1,x1)
	 --  invoke fuzzmemb[n2,XX2,mb2,MEMBER2,x2)
	 ----
	 MATRIX M12=mbr1*mbr2 |  --  joint membership functions
	 ------------------------------------------------------------------
	 Vector y=W3*m12 |  Vector error=tgt-y
	 DELTA W3=lratef*error*m12
	 ---------------------------------------              display varia
	 ERx10=10*abs(error[1])-scale
	 p1=x1[1] |  p2=x2[1]
	 m3=mbr1[3] |  m4=mbr1[4] |  m5=mbr1[5]
	 M3=mbr2[3]-scale |  M4=mbr2[4]-scale |  M5=mbr2[5]-scale
	 DISPXY tgt,y[1],tgt,ERx10,p1,m3,p1,m4,p2,M3,p2,M4
	 -----------------------------------------------------
	    label COMPETE
	 lratex=kappa*lratex+lrate0 |  -- reduce learning rate
	 Vector x=ran()
	 CLEARN v1=W1(x1)lratex,crit |  CLEARN v2=W2(x2)lratex,crit
	 delta h1=v1 |  delta h2=v2
	 Vector z1=W1%*v1 |  Vector z2=W2%*v2
	 dispxy z1[1],z2[1]

