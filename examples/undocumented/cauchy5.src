

	 -- Cauchy Distribution
	 -- fhas fat tails!
	 -- time starts at t=1 with xavg=x, goes to t=NN=n
	 ------------------------------------------------
	 display N1 |  display C8 |  display R
	 scale=2
	 NN=10000 |  --  maximum sample size
	 m=1000 |  -- number of replicated experiments
	 x0=0.00 |  -- true measured value
	 gamma=0.3
	 ARRAY x[m],xavg[m],xxavg[m],xvar[m],f[m],g[m]
	 --
	 --  xavg and xxavg are initialized to 0 by default
	 drun
	 write 'type go to continue' |  STOP
	 write "PLEASE WAIT!"
	 scale=30
	 -----------------------------------------------------
	    label PARZEN
	 h=0.07
	 alpha=1/(2*h*h) |  --   inverse window width
	 Ascale=3 |  B=Ascale/(gamma*PI) |  --   scale
	 aa=Ascale/sqrt(2*PI) |  beta=aa/(m*h)
	 scale=2
	 t=0 |  TMAX=20 |  NN=8000
	 drun READOUT
	 -----------------------------
	 DYNAMIC
	 -----------------------------
	 Vector x=gamma*tan(0.5*PI*ran()+x0)
	 Vector xavg=xavg+(x-xavg)/t
	 Vector xxavg=xxavg+(x^2-xxavg)/t
	 Vector xvar=xxavg-xavg^2
	 -----------------------------
	 AVG Xavg=xavg*1 |  AVG XXavg=xavg*xavg
	 Xvar=XXavg-Xavg^2
	 --
	 xvaro10=0.1*xvar[1] |  Xvarx100=100*Xvar
	 dispt Xavg,Xvarx100,xavg[1],xvaro10
	 ----------------------------------------------------------
	    label READOUT
	 --
	 xx=2*t-TMAX |  --                      display sweep
	 Q=B/(1+(xx/gamma)^2)-scale |  -- theoretical Cauchy
	 Vector f=beta*exp(alpha*(xx-x)*(x-xx))
	 Vector g=beta*exp(alpha*(xx-xavg)*(xavg-xx))
	 DOT F=f*1 |  F=F-scale |  -- x-distribution
	 DOT G=g*1 |  G=G-scale |  -- xavg-distribution
	 dispxy xx,Q,F,G

