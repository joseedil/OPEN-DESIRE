

	 --        EUROSIM PENDULUM-AND-PEG
	 -------------------------------------------------
	 NN=1000
	 m=1.02 |  l=1 |  lp=0.7 |  ls=0.3 |  g=9.81 |  d=0.2
	 kl=g/l |  ks=g/ls |  r=d/m |  a=l/ls |  A=1/a
	 Tmax=1 |  --               length of compound run
	 phip=-PI/12
	 TMAX=1 |  DT=0.001
	 display N1 |  display C8 |  --   display colors
	 -------------------------------------------------
	 irule 1 |  --             needed for later reruns
	 P=1 |  Q=0 |  --     switch to nonlinear pendulum
	 -------------------------------------------------
	    label START
	 phi=PI/6
	 --
	    label LONGSTR
	 k=kl |  ctrl=1 |  --                  long string
	 drun  |  display 2 |  --         keep display on!
	 --
	 if T>Tmax then go to QUIT |  else proceed
	 -------------------------------------------------
	 k=ks |  ctrl=-1 |  --                short string
	 phidot=phidot*a
	 drun
	 phidot=phidot*A |  --  run again with long string
	 --
	 go to LONGSTR
	 --
	    label QUIT
	 -------------------------------------------------
	 DYNAMIC
	 -------------------------------------------------
	 --                 	 run repeatedly, store results
	 --            		  P,Q switch nonlinear/linear cases
	 d/dt phi=phidot
	 d/dt phidot=-k*sin(phi)-r*phidot
	 term ctrl*(phip-phi) |  --         change length!
	 T=0.2*t-scale |  --             compound-run time
	 term T-Tmax |  --             end of compound run
	 dispxy T,phi

