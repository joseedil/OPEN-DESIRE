

	 --    FUNCTION-LEARNING BACKPROPAGATION NETWORK
	 ------------------------------------------------------------------
	 --          define generic 3-layer network with momentum
	 --          NOTE: definition does not depend on nx, nv, ny
	 --
	 ARRAY x$[1],y$[1],v$[1],deltav$[1],target$[1],error$[1]
	 ARRAY W1$[1,1],W2$[1,1],Dw1$[1,1],Dw2$[1,1]
	 --
	 SUBMODEL BPmom2(x$,y$,error$,target$,v$,deltav$,W1$,W2$,lrate1$,lra_
te2$,Dw1$,Dw2$,mom1$,mom2$)
	   --
	   Vector v$=tanh(W1$*x$)
	   Vector y$=W2$*v$
	   ----------------------------------------
	   Vector error$=target$-y$
	   Vector deltav$=W2$%*error$*(1-v$^2)
	   MATRIX Dw1$=lrate1$*deltav$*x$+mom1$*Dw1$
	   MATRIX Dw2$=lrate2$*error$*v$+mom2$*Dw2$
	   DELTA W1$=Dw1$ |  DELTA W2$=Dw2$
	   end
	 -------------------------------------------------       program st
	 display Q |  display N1 |  display C8 |  scale=0.5
	 nx=1 |  ny=1
	 nv=5 |  --                       number of hidden neurons
	 --                         
	 ARRAY x1[nx]+x0[1]=x |  x0[1]=1 |  --       unity bias term
	 ARRAY v[nv],y[ny],target[ny],error[ny],deltav[nv]
	 ARRAY WW1[nv,nx+1],W2[ny,nv],Dww1[nv,nx+1],Dw2[ny,nv]
	 ---------                       
	 for i=1 to nv |  --                                              
	   WW1[i,1]=0.2*ran() |  WW1[i,2]=0.2*ran() |  W2[1,i]=0.2*ran()
	   next
	 -------------------                        set experiment paramete
	 lrate1=1 |  lrate2=0.3
	 mom1=0.1 |  mom2=0.1
	 NN=10000 |  N=35
	 for i=1 to N |  drun  |  next  |  --  N training runs
	 write "type go for a test run" |  STOP
	 lrate1=0 |  lrate2=0 |  display R
	 drun  |  --                                                 recal
	 ------------------------------------------------------------------
	 DYNAMIC
	 ------------------------------------------------------------------
	 x1[1]=ran() |  target[1]=0.4*sin(4*x[1])
	 --
	 invoke BPmom2(x,y,error,target,v,deltav,WW1,W2,lrate1,lrate2,Dww1,D_
w2,mom1,mom2)
	 --
	 ERRx100=100*abs(error[1])-scale |  Xin=scale*x[1] |  --   displa
	 dispxy Xin,target[1],y[1],ERRx100

