

	 --   HEAT-CONDUCTION PARTIAL DIFFERENTIAL EQ.
	 --	IN CYLINDRICAL COORDINATES x is radius
	 --              x is radius, cylindrical symmetry (no theta)
	 ------------------------------------------------------------------
	 display N2 |  display C8 |  display R
	 ARRAY vx[1],v[1] |  --    dummy arrays for SUBMODEL
	 --
	 --          Schiesser numerical-differentiation operator
	 --
	 SUBMODEL DDx(n$,bb$,v,vx)
	   Vector vx=bb$*(v{1}-v{-1})
	   vx[1]=bb$*(-3*v[1]+4*v[2]-v[3])
	   vx[n$]=bb$*(3*v[n$]-4*v[n$-1]+v[n$-2])
	   end
	 ------------------------------------------------------------------
	 irule 15 |  ERMAX=0.001 |  -- Gear-type integration
	 n=51
	 STATE u[n] |  ARRAY x[n],ux[n],uxx[n],U[n]
	 ------------------------------------------------------------------
	 --
	 -- parameters:
	 R=1 |  u0=25 |  -- cylinder radius, surface temperatrure
	 scale=u0
	 --
	 DX=R/(n-1) |  bb=1/(2*DX)
	 DT0=0.00025 |  DT=DT0/(n^2)
	 TMAX=0.5 |  NN=1000
	 --
	 -- initial conditions u[i]=0 need not be programmed
	 for i=2 to n |  x[i]=(i-1)*DX |  next
	 x[1]=1 |  --     dummy initial setting prevents
	 --            trouble at x = 1, will be overwritten
	 u[1]=0
	 drun
	 ----------------------------------------------
	 DYNAMIC
	 ----------------------------------------------  
	 u[n]=u0 |  --        given boundary condition
	 invoke DDx(n,bb,u,ux) |  -- differentiate u to get ux
	 ux[1]=0 |  --               set boundary value
	 invoke DDx(n,bb,ux,uxx) |  -- differentiate ux to get uxx
	 Vectr d/dt u=uxx+ux/x |  --  note: not valid for x=0
	 d/dt u[1]=2*uxx[1] |  --     l'Hospital's rule
	 --
	 ------------------------------------        display
	 dt=4000*DT-scale
	 dispt u[1],u[0.1*n],u[0.2*n],u[0.3*n],u[0.4*n],u[0.5*n],u[0.6*n],u[_
0.7*n],u[0.8*n],u[0.9*n],u[n],dt

