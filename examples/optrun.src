

	 --        SERVO OPTIMIZATION BY MEASURED-GRADIENT METHOD
	 --   intentionally slowed for better display with fast computers!
	 ------------------------------------------------------------------
	 scale=0.25 |  display Q |  display N1 |  --    sets display 
	 DT=0.000004 |  TMAX=0.75 |  NN=4000
	 --------------------------------------------------------
	 u=0.5 |  --                     parameters and initial values
	 r=5 |  gain=300
	 opgain=50000 |  crit=0.00002 |  Dr=0.5 |  --   optimize
	 --
	 --            initial x,xdot, and ISE are 0, no need to program
	 --
	 ----------------------------  OPTIMIZATION EXPERIMENT
	 drun  |  --                    initial trial run
	 display 2 |  --             multiple runs on one plot
	 repeat
	   oldISE=ISE
	   r=r+Dr
	   reset  |  drun  |  --             run with perturbation
	   Derr=ISE-oldISE |  --                   gradient measurement
	   if abs(Derr)<crit then exit  |  --         we are done!
	     else proceed
	   r=r-Dr-opgain*Derr |  --                   working step 
	   reset  |  drun
	   until 0>1 |  --                                   keep trying
	 --------------------------------------------------------
	 reset  |  display R |  display N12 |  drunr  |  -- change colo
	 write 'type go to see optimal solution' |  STOP
	 display F |  drun
	 write 'optimal value: r=';r,'ISE=';ISE
	 --------------------------------------------------------
	 DYNAMIC
	 -- -------------------------------------          MODEL EQUATIONS
	 e=x-u
	 d/dt x=xdot |  d/dt xdot=-gain*e-r*xdot
	 d/dt ISE=e*e |  --                 mean square errorl
	 xx=2*x-1 |  dispt xx |  --           offset runtime display

