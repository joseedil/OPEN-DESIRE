

	 -- 2-DIMENSIONAL TORPEDO SIMULATION
	 -- statistics over n effective simulation runs
	 --------------------------------------
	 display N1 |  display C8 |  scale=2.5
	 DT=0.001 |  TMAX=48 |  NN=1500
	 irule 4 |  ERRMAX=0.1
	 -------------------------------------------------------
	 ARRAY RT[48] |  -- RUDDER-COMMAND BREAKPOINT TABLES
	 data 19,20,25,26 |  --                                     circle
	 data 32,33,34,40,41,42,47,48,49 |  --            snake
	 data 55,56,57,63,64,65,71,72,73
	 data 74,75 |  --                                               at
	 ------------------------
	 data 0,0.25,0.25,0 |  --                                   circle
	 data 0,-0.25,0,0,0.25,0,0,-0.25,0,0,-0.25 |  -- snake
	 data 0,0,-0.25,0,0,0.25,0
	 data 0,-0.125 |  --                                           att
	 read RT
	 -------------------------------------------------------
	 n=500
	 STATE u[n],v[n],psi[n],psidot[n],x[n],y[n] |  -- state variables
	 ARRAY cosp[n],sinp[n],rr[n] |  --                 defined variabl
	 ARRAY rrnoise[n] |  --                noisy steering 
	 ------------------------------------------------------
	 --              TORPEDO PARAMETERS
	 UC=0.25
	 X1=0.8155 |  X2=0.8155 |  --          u-equation parameters
	 UT=X1*UC*UC
	 Y1=-15.701 |  Y2=-0.23229 |  Y3=2.0002 |  --      v-params.
	 N1=-303.801 |  N2=-44.866 |  N3=-243.866 |  --    r-params.
	 noise=0.001 |  --                                     noise fract
	 ------------------------------------------------------------------
	 for k=1 to n |  x[k]=-2.4 |  y[k]=2.2 |  --       initial value
	   --
	   --        noise-perturbed steering command
	   --
	   rrnoise[k]=noise*(ran()+ran()+ran()+ran())
	   next
	 --
	 drun
	 ------------------------------------------------------------------
	 DYNAMIC
	 ------------------------------------------------------------------
	 func rudder=RT(t |  --         rudder command function
	 Vector rr=(rudder+rrnoise)*u |  --      noise-perturbed!
	 --
	 Vectr d/dt u=UT-X2*u*u
	 Vectr d/dt v=u*(Y1*v+Y2*psidot+Y3*rr)
	 Vectr d/dt psidot=u*(N1*v+N2*psidot+N3*rr)
	 Vectr d/dt psi=psidot
	 --
	 Vector cosp=cos(psi) |  Vector sinp=sin(psi)
	 Vectr d/dt x=u*cosp-v*sinp |  Vectr d/dt y=u*sinp+v*cosp
	 --------------------------------------------------------
	 --                                  compute Monte Carlo statistics
	 --
	 DOT xsum=x*1 |  DOT ysum=y*1
	 DOT xxsum=x*x |  DOT yysum=y*y
	 --
	 xavg=xsum/n |  yavg=ysum/n
	 varx=xxsum/n-xavg*xavg |  vary=yysum/n-yavg*yavg
	 sigmax=sqrt(abs(varx)) |  sigmay=sqrt(abs(vary))
	 --
	 DISPXY xavg,yavg,x[1],y[1],x[3],y[3],x[4],y[4],x[5],y[5]

