

	 --                                 COMPETITIVE LEARNING
	 -------------------------------------------------------
	 m=2 |  --                              number of inputs
	 n=4 |  --             no. of competitive-layer neurons           
	 --
	 ARRAY x[m],v[n],h[n],z[m],W[n,m]
	 ARRAY z1[2],z2[2],z3[2],z4[2]
	 -------------------------------------------------------
	 for i=1 to n
	   for k=1 to m |  --        set random initial weights
	     W[i,k]=0.01*ran()
	     next
	   h[i]=0 |  --          initialize conscience (if any)
	   next
	 display R |  display N5 |  display C8
	 ------------------------------------------------------
	 --                           set experiment parameters
	 crit=-1 |  --                             CLEARN  mode
	 lrate=0.08 |  kappa=0.95 |  lrate0=0.002
	 noise=1
	 NN=8000
	 ------------------------------------------------------
	 drun  |  --                 "continued" runs show
	 drun  |  drun  |  --              progress more clearly!
	 --
	 write 'type go for a check run' |  STOP
	 ------------------------------------------------------
	 --                                   make a recall run
	 lrate=0 |  lrate0=0
	 for i=1 to n |  h[i]=1.0E-25 |  next
	 drun
	 ONE=0 |  H=0 |  --     initialize checksum and entropy
	 a=1/NN
	 for i=1 to n
	   h[i]=a*h[i] |  --   statistical relative frequencies
	   ONE=ONE+h[i] |  --               accumulate checksum
	   H=H-h[i]*ln(h[i]) |  --           accumulate entropy
	   next
	 -----------------------------          display results
	 write h |  write
	 write 'H =  ';H/ln(2);' bits','      checksum =  ';ONE
	 write 'log2(n) = ';ln(n)/ln(2)
	 STOP
	 ------------------------------------------------------
	    label eee
	 --                            parameter-editing screen
	 edit 260-270,295,2050-2090
	 ------------------------------------------------------
	 DYNAMIC
	 ------------------------------------------------------
	 --                                 weight-learning run
	 lrate=lrate*kappa+lrate0 |  --     decrease learn rate
	 iRow=t
	 VECTOR x=noise*ran()
	 CLEARN v=W(x)lrate,crit |  --            compete,learn
	 UPDATE h=v |  --                      FSCL when crit=0
	 VECTOR z1=v[1]*x |  VECTOR z2=v[2]*x
	 VECTOR z3=v[3]*x |  VECTOR z4=v[4]*x
	 DISPXY z1[1],z1[2],z2[1],z2[2],z3[1],z3[2],z4[1],z4[2]

