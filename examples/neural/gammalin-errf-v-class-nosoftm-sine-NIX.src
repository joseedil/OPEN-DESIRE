

	 --   NONLINEAR GAMMA-LINE CLASSIFIER
	 --------------------------------------------------------------
	 display N3 |  display C8 |  display R |  scale=4
	 TMAX=1 |  NN=50000
	 --------------------------------------------------------------
	 nx=10 |  --       delay-line size
	 nv=17 |  --      number of hidden-layer neurons
	 ARRAY y[2],deltay[2],target[2],error[2]
	 ARRAY x[nx]+x0[1]+v[nv]=xx |  x0[1]=1
	 ARRAY deltav[nv]
	 ARRAY WW1[nv,nx+nv+1],W2[2,nv]
	 --
	 for i=1 to nv |  for k=1 to nx+nv+1 |  -- initialize
	     WW1[i,k]=0.1*ran()
	     next  |  next
	 --------------
	 lrate1=0.01 |  lrate2=0.007
	 noise=0.00
	 mu=0.5 |  --                for gamma delay line
	 N=20 |  --               number of training runs
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for recall test' |  STOP
	 lrate1=0 |  lrate2=0 |  drun  |  -- test run
	 -----------------------------------------------------------
	 DYNAMIC
	 ----------------------------------------------------------
	 switch=swtch(sin(10*t)) |  --  alternate patterns
	 p=0.8*switch*cos(300*t) |  -- sine waves
	 q=(1-switch)*sin(100*t) |  --   sine waves
	 input=p+q+noise*ran()
	 target[1]=p |  target[2]=q
	 ----
	 Vector x=x+mu*(x{-1}-x) |  x[1]=input |  --  gamma line
	 Vector v=tanh(WW1*xx) |  --               includes bias
	 Vector y=tanh(W2*v)
	 --
	 Vector error=target-y
	 Vector deltay=error*(1-y^2) |  --      backprop
	 Vector deltav=W2%*error*(1-v^2)
	 DELTA WW1=lrate1*deltav*xx
	 DELTA W2=lrate2*deltay*v
	 ----------------------------------------- rescaled stripchart
	 X=x[1]+0.7*scale |  ERROR=error[1]-0.5*scale
	 Y=y[1] |  TARGET=target[1]+0.5*scale
	 dispt TARGET,Y,ERROR

