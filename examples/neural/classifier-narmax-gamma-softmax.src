

	 --   NARMAX/GAMMA-LINE CLASSIFIER
	 --------------------------------------------------------------
	 display N3 |  display C8 |  display R |  scale=4
	 TMAX=1 |  NN=100000
	 --------------------------------------------------------------
	 nx=15 |  --     number of gamma-line neurons
	 nf=15
	 nv=17 |  --      number of hidden-layer neurons
	 --
	 ARRAY y[2],error[2],P[2],target[2]
	 ARRAY x[nx]+x0[1]+feedback[nf]=xx |  x0[1]=1
	 ARRAY v[nv],deltav[nv]
	 ARRAY WW1[nv,nx+nf+1],WW2[2,nv]
	 --
	 for i=1 to nv |  for k=1 to nx+nf+1 |  -- initialize
	     WW1[i,k]=0.1*ran()
	     next  |  next
	 -----------------------------------------------------------
	 lrate1=0.06 |  lrate2=0.05
	 noise=0.00
	 mu=0.6 |  --                for gamma delay line
	 --
	 N=4 |  --                       number of training runs
	 for i=1 to N |  drun  |  next
	 ---------
	 write 'type go for recall test' |  STOP
	 lrate1=0 |  lrate2=0 |  drun  |  -- test run
	 -----------------------------------------------------------
	 DYNAMIC
	 ----------------------------------------------------------
	 switch=swtch(sin(10*t)) |  --  alternate patterns
	 p=0.8*switch*cos(200*t) |  --             sine waves
	 q=(1-switch)*sgn(sin(100*t)) |  --  square waves
	 input=p+q+noise*ran()
	 target[1]=switch |  target[2]=1-switch
	 ----
	 Vector x=x+mu*(x{-1}-x) |  x[1]=input |  --  gamma line
	 Vector v=tanh(WW1*xx) |  --               includes bias
	 Vector y=exp(WW2*v) |  DOT ysum=y*1 |  -- softmax
	 Vector P=y/ysum
	 --
	 Vector error=target-P
	 Vector deltav=WW2%*error*(1-v^2) |  -- backprop
	 DELTA WW1=lrate1*deltav*xx
	 DELTA WW2=lrate2*error*v
	 --
	 Vector feedback=feedback+mu*(feedback{-1}-feedback)
	 feedback[1]=error[1]
	 ----------------------------------------- rescaled stripchart
	 X=x[1]+0.7*scale |  errorx10=10*error[1]-0.5*scale
	 dispt X,P[2],errorx10

