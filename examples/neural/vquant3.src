

	 --                                COMPETITIVE LEARNING
	 ---------------------------------------------------------------
	 nx=2 |  --                             number of inputs
	 nv=25 |  --  number of competitive-layer neurons
	 --
	 ARRAY x[nx],v[nv],h[nv],w[nx],W[nv,nx]
	 --
	 --                          set random initial weights
	 for i=1 to nv
	   h[i]=0 |  --         initialize conscience counters
	   for k=1 to nx
	     W[i,k]=0.01*ran()
	     next
	   next
	 display R |  display N0 |  display C8 |  scale=2.5
	 ---------------------------------------------------------------
	 --                           set experiment parameters
	 lrate=.05 |  --                                     learn rate
	 kappa=0.995 |  lrate0=0.0001 |  --  decay lrate
	 crit=0 |  --                              set CLEARN mode
	 NN=2000
	 ------------------------------------------------------
	 drun  |  --                 simulation run (NN trials)
	 write "type go to continue" |  STOP
	 NNold=NN |  NN=5000
	 drun  |  --                              run shows results
	 write 'type go for a check run' |  STOP
	 NN=1000 |  a=1/NN
	 lrate=0 |  lrate0=0
	 for i=1 to nv |  h[i]=1.0E-25 |  next
	 drun
	 ONE=0 |  H=0 |  --     initialize checksum and entropy
	 for i=1 to nv
	   h[i]=a*h[i] |  --   statistical relative frequencies
	   ONE=ONE+h[i] |  --               accumulate checksum
	   H=H-h[i]*ln(h[i]) |  --           accumulate entropy
	   next
	 -----------------------------          display results
	 write h |  write
	 write 'H =  ';H/ln(2);' bits','        checksum =  ';ONE
	 write  |  write 'log2(nv) = ';ln(nv)/ln(2)
	 write 'type go to see the template array' |  STOP
	 write  |  write W
	 ------------------------------------------------------
	 DYNAMIC
	 -------------------------------------------------------
	 Vector x=gauss(0)
	 CLEARN v=W(x)lrate,crit |  --      compete,learn
	 Vectr delta h=v |  --                            conscience
	 Vector w=W%*v |  --        reconstruct templates
	 ------------------------------------------------------
	 lrate=lrate0+lrate*kappa |  --    decrease learn rate
	 DISPXY x[1],x[2],w[1],w[2]

