

	 --    10-BIT ENCODER LEARNING, creeping random search
	 ----------------------------------------------------------
	 --  22550 synapses
	 display N1 |  display C8 |  display R
	 nv=10 |  --         number of hidden-layer neurons
	 N=1024 |  --      number of network inputs
	 --
	 ARRAY x[N]+x0[1]=xx |  x0[1]=1
	 ARRAY v[nv]+v0[1]=vv |  v0[1]=1
	 ARRAY y[N],error[N]
	 ------------
	 ARRAY W1[nv,N+1],W2[N,nv+1]
	 ARRAY DELW1[nv,N+1],DELW2[N,nv+1]
	 ARRAY DW1[nv,N+1],DW2[N,nv+1]
	 ARRAY delW1[nv,N+1],delW2[N,nv+1]
	 ------------------------------------------------------------------
	 fsav=1000 |  fmin=1000 |  --   initialize optimization
	 ------------------------------------------------------------------
	 --                                                  set parameters
	 gain=0.001 |  gain1=0.07 |  gain2=0.07
	 mom1=0.2 |  mom2=0.2 |  crit=0.0002 |  c=1.6
	 scale=0.5 |  NN=10000
	 ------------------------------------------------------------------
	 x[5]=1 |  -- start binary selector patterns
	 drun  |  --           simulation runs (NN trials) 
	 write fmin,measure,checksum
	 write "type go to continue" |  STOP
	 write "PLEASE WAIT!"
	 scale=fmin |  gain=gain/20
	 gain1=gain1 |  gain2=gain2
	 crit=0.00001
	 drun
	 write fmin,measure,checksum
	 -----------------------------------------------------------
	 DYNAMIC
	 -----------------------------------------------------------
	 --                                      weight-learning run
	 q=x[N] |  -- make successive binary selector patterns
	 Vector x=x{-1} |  x[1]=q |  -- recirculate selector
	 Vector v=W1*xx
	 Vector y=exp(c*W2*vv)
	 DOT ysum=y*1 |  Vector y=y/ysum
	 --
	 Vector error=x-y
	 DOT measure=error*error
	 DOT checksum=y*1
	 -------------------------------------------------------
	 term crit-measure |  --                terminate run on success
	 delf=comp(fsav-measure,-1,gain) |  --  kills unsuccessful changes
	 fsav=measure |  --                                   save measure
	 FSAV=fsav-scale |  --                  offset the curves
	 ------------------------------------------------------------
	 --                                            	      update
	 MATRIX DW1=delW1
	 MATRIX DW2=delW2
	 MATRIX delW1=delf*DELW1 |  -- note order!
	 DELTA W1=delW1+mom1*DW1 |  --    note use of previous value
	 DELTA W2=delf*DELW2+mom2*DW2
	 --------------------------------------------------------
	 --                       try pseudorandom perturbations
	 --
	 MATRIX DELW1=gain1*ran()
	 MATRIX DELW2=gain2*ran()
	 DELTA W1=DELW1 |  DELTA W2=DELW2
	 --------------------------------------------------------
	 if fmin-fsav
	   fmin=fsav
	   FMIN=fmin-scale |  --                 offset the curve
	   -------------------------------------------
	   dispt FMIN,FSAV |  --  display error measures
	   -----------------------------------------------------
	   --                                         test run
	      label TEST
	   iRow=t |  Vector x=INPUT#
	   Vector v=W1*xx
	   Vector y=exp(c*W2*vv)
	   DOT ysum=y*1 |  Vector y=y/ysum
	   -----------------------------
	   Vector error=x-y
	   DOT measure=error*error
	   DOT checksum=y*1
	   dispt FMIN,FSAV |  --  display error measures

